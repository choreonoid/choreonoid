# Use the following variables to compile and link against Choreonoid:
# CHOREONOID_FOUND            - True if Choreonoid was found on your system
# CHOREONOID_VERSION_STRING   - A human-readable string containing the version
# CHOREONOID_VERSION_MAJOR    - The major version of Choreonoid
# CHOREONOID_VERSION_MINOR    - The minor version of Choreonoid
# CHOREONOID_VERSION_PATCH    - The patch version of Choreonoid
# CHOREONOID_ROOT_DIR         - The base directory of Choreonoid
# CHOREONOID_CXX_STANDARD     - The c++ standard version used to build Choreonoid (11, 14, or 17)
# CHOREONOID_DEFINITIONS      - Definitions needed to build with Choreonoid
# CHOREONOID_INCLUDE_DIRS     - List of directories of Choreonoid and it's dependencies
# CHOREONOID_LIBRARY_DIRS     - List of directories of Choreonoid and it's dependencies
# CHOREONOID_LIBRARIES        - List of directories of Choreonoid and it's dependencies

set(CHOREONOID_ROOT_DIR "@CNOID_DIR@")

set(CHOREONOID_VERSION_STRING "@CNOID_FULL_VERSION@")
set(CHOREONOID_VERSION_MAJOR "@CNOID_MAJOR_VERSION@")
set(CHOREONOID_VERSION_MINOR "@CNOID_MINOR_VERSION@")
set(CHOREONOID_VERSION_PATCH "@CNOID_PATCH_VERSION@")

set(CHOREONOID_CXX_STANDARD @CMAKE_CXX_STANDARD@)
set(CHOREONOID_DEFINITIONS @definitions@)
set(CHOREONOID_INCLUDE_DIRS @CNOID_SDK_INCLUDE_DIRS@)
set(CHOREONOID_LIBRARY_DIRS @library_dirs@)
set(CHOREONOID_UTIL_LIBRARIES CnoidUtil)
set(CHOREONOID_BASE_LIBRARIES ${CHOREONOID_UTIL_LIBRARIES} CnoidBase)
set(CHOREONOID_BODY_LIBRARIES ${CHOREONOID_UTIL_LIBRARIES} CnoidBody)
set(CHOREONOID_BODY_PLUGIN_LIBRARIES ${CHOREONOID_BASE_LIBRARIES} CnoidBody CnoidBodyPlugin)
set(CHOREONOID_LIBRARIES ${CNOD_BODY_LIBRARIES} ${CHOREONOID_BODY_PLUGIN_LIBRARIES})
set(CHOREONOID_PLUGIN_SUBDIR @CNOID_PLUGIN_SUBDIR@)
set(CHOREONOID_PLUGIN_DIR @CNOID_DIR@/@CNOID_PLUGIN_SUBDIR@)
set(CHOREONOID_SHARE_SUBDIR @CNOID_SHARE_SUBDIR@)
set(CHOREONOID_SHARE_DIR @CNOID_DIR@/@CNOID_SHARE_DIR@)

set(CHOREONOID_ENABLE_PYTHON @ENABLE_PYTHON@)
set(CHOREONOID_PYTHON_LIBRARIES CnoidPyUtil CnoidPyBase)
set(CHOREONOID_PYTHON_SUBDIR @CNOID_PYTHON_SUBDIR@)
set(CHOREONOID_USE_PYBIND11 @USE_PYBIND11@) # This variable is kept for backward compatibility

MARK_AS_ADVANCED(
  CHOREONOID_ROOT_DIR
  CHOREONOID_INCLUDE_DIRS
  CHOREONOID_LIBRARIES
  CHOREONOID_Boost_VERSION
  CHOREONOID_CFLAGS_OTHER
  CHOREONOID_LDFLAGS_OTHER
  CHOREONOID_LIBRARY_SUFFIX
  )

function(add_cnoid_plugin)
  set(target ${ARGV0})
  add_library(${ARGV})
  get_target_property(compile_flags ${target} COMPILE_FLAGS)
  if(compile_flags)
    set_target_properties(${target} PROPERTIES COMPILE_FLAGS "${compile_flags} -fvisibility=hidden")
  else()
    set_target_properties(${target} PROPERTIES COMPILE_FLAGS "-fvisibility=hidden")
  endif()
  set_target_properties(${target} PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY ${CHOREONOID_PLUGIN_SUBDIR}
    ARCHIVE_OUTPUT_DIRECTORY ${CHOREONOID_PLUGIN_SUBDIR}
    RUNTIME_OUTPUT_DIRECTORY ${CHOREONOID_PLUGIN_SUBDIR})
endfunction()

function(cnoid_make_gettext_mofiles target out_mofiles)
  set(${out_mofiles} "" PARENT_SCOPE)
  file(GLOB pofiles ${CMAKE_CURRENT_SOURCE_DIR}/po/*.po)
  foreach(pofile ${pofiles})
    get_filename_component(lang ${pofile} NAME_WE)
    set(message_location share/locale/${lang}/LC_MESSAGES)
    file(MAKE_DIRECTORY ${PROJECT_BINARY_DIR}/${message_location})
    set(version "${CHOREONOID_VERSION_MAJOR}.${CHOREONOID_VERSION_MINOR}")
    set(mofile ${PROJECT_BINARY_DIR}/${message_location}/${target}-${version}.mo)
    add_custom_command(
      OUTPUT ${mofile}
      COMMAND msgfmt -o ${mofile} ${pofile}
      DEPENDS ${pofile}
      )
    list(APPEND mofiles ${mofile})
    install(FILES ${mofile} DESTINATION "share/locale/${lang}/LC_MESSAGES")
  endforeach()
  set(${out_mofiles} ${mofiles} PARENT_SCOPE)
endfunction()

if(CHOREONOID_ENABLE_PYTHON)
  function(add_cnoid_python_module)
    set(target ${ARGV0})
    string(REGEX REPLACE "^Py(.+)$" "\\1" module ${target})
    set(sources ${ARGV})
    list(REMOVE_AT sources 0)
    add_library(${target} SHARED ${sources})
    if(MSVC)
      set_target_properties(${target} PROPERTIES SUFFIX .pyd)
    else()
      get_target_property(compile_flags ${target} COMPILE_FLAGS)
      if(compile_flags)
	set_target_properties(${target} PROPERTIES COMPILE_FLAGS "${compile_flags} -fvisibility=hidden")
      else()
	set_target_properties(${target} PROPERTIES COMPILE_FLAGS "-fvisibility=hidden")
      endif()
      if(NOT ${CMAKE_BUILD_TYPE} MATCHES Debug)
	if(CMAKE_CXX_COMPILER_ID MATCHES "GNU")
	  target_compile_options(${target} PRIVATE "-flto;-fno-fat-lto-objects")
	  set(link_lto_flags "-flto")
	elseif(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
	  target_compile_options(${target} PRIVATE "-flto=thin")
	  set(link_lto_flags "-flto=thin")
	endif()
	get_target_property(existing_link_flags ${target} LINK_FLAGS)
	if(existing_link_flags STREQUAL existing_link_flags-NOTFOUND)
	  set(existing_link_flags "")
	endif()
	set_target_properties(${target} PROPERTIES LINK_FLAGS "${existing_link_flags} ${link_lto_flags}")
        #add_custom_command(TARGET ${target} POST_BUILD COMMAND strip $<TARGET_FILE:${target}>)
      endif()
    endif()
    set_target_properties(${target}  PROPERTIES
      RUNTIME_OUTPUT_DIRECTORY ${CHOREONOID_PYTHON_SUBDIR}/cnoid
      LIBRARY_OUTPUT_DIRECTORY ${CHOREONOID_PYTHON_SUBDIR}/cnoid
      RUNTIME_OUTPUT_NAME ${module}
      LIBRARY_OUTPUT_NAME ${module}
      PREFIX "")
    install(TARGETS ${target}
      RUNTIME DESTINATION ${CHOREONOID_PYTHON_SUBDIR}/cnoid
      LIBRARY DESTINATION ${CHOREONOID_PYTHON_SUBDIR}/cnoid)
    endfunction()
endif()
